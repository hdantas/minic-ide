module trans/codegen/stmt

imports
  libstratego-gpp
  lib/runtime/tmpl/pp
  include/MiniC
  include/MiniC-parenthesize
  runtime/nabl/-
  runtime/task/-
  runtime/types/-
  analysis/desugar
  analysis/names
  lib/jasmin/JasminXT
  
  trans/codegen/type
  trans/codegen/exp


rules // STATEMENTS 
  
  // Recursive call when multiple statements exist
  stmt-to-jbc: [s, t | ts] -> instr*
  with
    head-stmt* := <stmt-to-jbc> s
  ; tail-stmt* := <stmt-to-jbc> [t | ts]
  ; instr*     := [head-stmt*, tail-stmt*]
  
  stmt-to-jbc: [s] -> <stmt-to-jbc> s
  stmt-to-jbc: []  -> []
  

  stmt-to-jbc: PrintfInt(i) -> instr
  with
    instr := <printf-aux> i
  
  stmt-to-jbc: PrintfFloat(f) -> instr
  with
    instr := <printf-aux> f
  
  printf-aux: expression -> instr*
  with
    static := GETSTATIC(JBCFieldRef(CRef("java/lang/System"), FRef("out"), JBCFieldDesc(Reference(CRef("java/io/PrintStream")))))
  ; exp*   := <exp-to-jbc> expression
  ; invoke := INVOKEVIRTUAL(JBCMethodRef(CRef("java/io/PrintStream"), MRef("println"), JBCMethodDesc(<get-type> expression, Void())))
  ; instr* := [static, exp*, invoke]
  
  stmt-to-jbc: CodeBlock(c) -> [<stmt-to-jbc> c]

  stmt-to-jbc: IfElse(cond, if-stmt, else-stmt) -> instr*
  with
    else-label     := <newname>
  ; end-label      := <newname>
  ; jbc-cond*      := <exp-to-jbc> cond
  ; jbc-if-stmt*   := <stmt-to-jbc> if-stmt
  ; jbc-else-stmt* := <stmt-to-jbc> else-stmt  
  ; instr*         := [
      jbc-cond*
    , IFEQ(LabelRef(else-label))
    , jbc-if-stmt*
    , GOTO(LabelRef(end-label))
    , JBCLabel(else-label)
    , jbc-else-stmt*
    , JBCLabel(end-label)
    ]
  
            
  stmt-to-jbc: While(cond, stmt) -> instr*
  with
    cond-label := <newname>
  ; loop-label := <newname>
  ; end-label  := <newname>
  ; jbc-stmt*  := <stmt-to-jbc> stmt
  ; jbc-cond*  := <exp-to-jbc> cond
  ; instr*     := [
    GOTO(LabelRef(cond-label))
    , JBCLabel(loop-label)
    , jbc-stmt*
    , JBCLabel(cond-label)
    , jbc-cond*
    , IFNE(LabelRef(loop-label))
    , JBCLabel(end-label)
    ]
 
    
rules // Generate Code for Local Variables and Variable Access

  stmt-to-jbc: VarDec(Struct(ty), name) -> instr //instr
  with
    struct-name := $[HelloWorld/[ty]]
  ; new-object := NEW(CRef(struct-name))
  ; constructor := INVOKESPECIAL(
	    JBCMethodRef(
	    CRef(struct-name)
	    , MRef(Init())
	    , JBCMethodDesc(
	        [Reference(CRef("HelloWorld"))]
	      , Void()
	      )
	    )
    )
  ; index    := <get-var-index> name
  ; store    := <Snd> index //retrieve store instr from index 
  ; instr    := [new-object, DUP(), ALOAD_0(), constructor, store]

    
  stmt-to-jbc: VarDec(ty, name) -> []
  stmt-to-jbc: ArrayDec(ty, name, size) -> instr*
  with
    load-size*  := <exp-to-jbc> IntConst(<dec-string-to-int> size) // array size is always an integer constant
  ; newarray    := NEWARRAY(<type-array-to-jbc> ty) // NEWARRAY(IntName()) 
  ; var-index  := <get-var-index> name // fails for non local var
  ; store-jbc* := <Snd> var-index //retrieve store instr from index
  ; instr*     := [load-size*, newarray, store-jbc*]


rules //assignments Assignment(Var("a"), IntConst("1"))
  stmt-to-jbc: Assignment(StructMember(var, ArrayField(fname, findex)), exp) -> instr* // assignments to array fields
  with 
    varaccess*   := <exp-to-jbc> StructMember(var, ArrayField(fname, findex))
  ; expaccess*   := <exp-to-jbc> exp
  ; store-field* := <get-type; array-store-aux> ArrayField(fname, findex)
  ; instr*       := [varaccess*, expaccess*, store-field*]  
    

  stmt-to-jbc: Assignment(StructMember(var, Field(f)), exp) -> instr* //assignments to non-array fields
  with 
    [varaccess*, fieldref]   := <exp-to-jbc> StructMember(var, Field(f))
  ; expaccess*   := <exp-to-jbc> exp
  ; storefield   := PUTFIELD(<get-arguments; index(|1)> fieldref)
  ; instr*       := [varaccess*, expaccess*, storefield]

  
  stmt-to-jbc: Assignment(ArrayVar(arrayname, index), exp) -> instr*
  with // Assignment(ArrayVar("global", IntConst("3")), IntConst("0"))
    varaccess*   := <exp-to-jbc> ArrayVar(arrayname, index)
  ; expaccess*   := <exp-to-jbc> exp
  ; store-field* := <get-type; array-store-aux> ArrayVar(arrayname, index)
  ; instr*       := [varaccess*, expaccess*, store-field*]
    
  array-store-aux: Int() ->   [IASTORE()]
  array-store-aux: Float() -> [FASTORE()]
  
  stmt-to-jbc: Assignment(var, exp) -> instr
  with
  //only local vars have indices
    instr := <if(<get-var-index> var, <local-stmt-to-jbc> Assignment(var, exp), <global-stmt-to-jbc> Assignment(var, exp))>
    
  local-stmt-to-jbc: Assignment(var, exp) -> instr* // assignment to local var
  with
    var-index  := <get-var-index> var // fails for non local var
  ; exp-jbc*   := <exp-to-jbc> exp
  ; store-jbc* := <Snd> var-index //retrieve store instr from index
  ; instr*     := [exp-jbc*, store-jbc*]
    
  global-stmt-to-jbc: Assignment(var, exp) -> instr* // assignment to global var
  with
    expaccess* := <exp-to-jbc> exp
  ; var-jbc    := <exp-to-jbc> var
  ; varaccess  := <get-arguments; index(|1); get-arguments; index(|1)> var-jbc //remove outer "GETSTATIC()"
  ; instr*     := [expaccess*, PUTSTATIC(varaccess)]
    
rules // auxiliary
  // get-field-or-const: [IntConst(_), jbc]   -> jbc // dont use GETSTATIC() for constants 
  // get-field-or-const: [FloatConst(_), jbc] -> jbc // dont use GETSTATIC() for constants
  // get-field-or-const: [var, jbc]           -> instr
  // with
  //   instr := <if(<get-var-index> var, <local-access> jbc, <field-access> jbc)>
  //     
  // field-access: jbc -> GETSTATIC(jbc)
  // local-access: jbc -> jbc
