module MiniC-ContextFree

  imports
    MiniC-Lexical
 
 
 /*
 *
 *  Indentiation Style: Whitesmiths
 *  http://en.wikipedia.org/wiki/Indent_style#Whitesmiths_style
 *  
 */
 
  templates
  
    Program.File                      = <<TypeDec*; hide, separator="\n">
<VarDec*; hide, separator="\n">
<FunctionDec*; hide, separator="\n">
<Exp>
>
    
    TypeDec.StructDec                 = <struct <ID; text="StructName">
  {
  <FieldDec*; text="StructMembers", separator="\n">
  };>

	  FieldDec.FieldDec                 = <<Type; text="Type"> <ID; text="VarName">;>
    FieldDec.FieldArrayDec            = <<ArrayType; text="Type"> <ID; text="ArrayName">[<INT; text="NumberElements">];>
    VarDec.VarDec                     = <<Type; text="Type"> <ID; text="VarName">;>
    VarDec.ArrayDec                   = <<ArrayType; text="Type"> <ID; text="ArrayName">[<INT; text="NumberElements">];>
    
    FunctionDec.FunctionDef           = <<Type; text="ReturnType"> <ID; text="FuncName"> (<Parameter*; separator=", ", text="Arguments">)
  {
  <Statement*; hide, text="Body", separator="\n">
  return <Exp; text="Return value">;
  }>
    
    Parameter.Arg                     = <<Type; text="Type"> <ID; text="Name">>
    Parameter.ArrayArg                = <<ArrayType; text="Type"> <ID; text="Name">[]>
    
    ArrayType.Int                     = <int>
    ArrayType.Float                   = <float>
    Type.Int                          = <int>
    Type.Float                        = <float>
    Type.Struct                       = <struct <ID; text="StructName">>

    Statement                         = <<VarDec>>
    Statement.Assignment              = <<Lvalue; text="Var"> = <Exp>;>
    //Statement.ArrayAssignment         = <<Lvalue; text="ArrayName">[<Exp; text="Index">] = <Exp>;>
    // Statement.StructMemberAssignment  = <<Lvalue; text="StructName">.<ID; text="StructMember"> = <Exp>;>
    Statement.IfElse                  = <if (<Exp; text="Condition">)
  <Statement>
else
  <Statement>

>
  
   Statement.While                   = <while (<Exp; text="Condition">)
  <Statement>

>

    Statement.PrintfInt               = <printf("%i", <Exp; text="IntegerVar">);>
    Statement.PrintfFloat              = <printf("%f", <Exp; text="FloatVar">);>
    Statement.CodeBlock               = <{
<Statement*; separator="\n">
}>
    
    Exp.OctalConst                    = <<OCTAL; hide>>
    Exp.IntConst                      = <<INT; hide>>
    Exp.FloatConst                    = <<FLOAT; hide>>
    
    Lvalue.Var                        = <<ID; text="VarName">>
    // Field.Field                       = <<ID; text="VarName">>
    Lvalue.StructMember               = <<Lvalue; text="StructName">.<Lvalue; text="StructMember">> {left}
    Lvalue.ArrayVar                   = <<ID; text="ArrayName">[<Exp; text="Index">]>
    Exp                               = <<Lvalue>>
    
    
    Exp.And                           = <<Exp; text="Value"> && <Exp; text="Value">> {left}
    Exp.Lt                            = [[Exp; text="Value"] < [Exp; text="Value"]] {non-assoc}
  //Exp.Gt                            = [[Exp; text="Value"] > [Exp; text="Value"]] {non-assoc}
    Exp.Add                           = <<Exp; text="Addend"> + <Exp; text="Addend">> {left}
    Exp.Subtract                      = <<Exp; text="Minuend"> - <Exp; text="Subtrahend">> {left}
    Exp.Multiply                      = <<Exp; text="Multiplicand"> * <Exp; text="Multiplier">> {left}
  //Exp.Divide                        = <<Exp; text="Dividend"> / <Exp; text="Divisor">> {left}
    Exp.Not                           = <!<Exp; text="Boolean">>
    Exp                               = <(<Exp; text="Expression">)> {bracket}
    Exp.Sizeof                        = <sizeof (<Exp; text="Datatype">)>
    Exp.Call                          = <<ID; text="FuncName">(<Exp*; separator=", ">)>
    Exp.Uminus                        = <-<Exp>>
    Exp.Uplus                         = <+<Exp>>
  
  context-free priorities
      
      {
        Exp.Uminus
        Exp.Uplus
      } >  { left:
        Exp.Multiply
        // Exp.Divide
      } >  { left:
        Exp.Add
        Exp.Subtract
      } > { non-assoc:
        Exp.Lt
        // Exp.Gt
      } > { left:
        Exp.And
      }
      
    template options
      keyword -/- [a-zA-Z]
      tokenize : "\""
    
    