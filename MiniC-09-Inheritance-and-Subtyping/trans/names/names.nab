module names/names

imports
  include/MiniC
  names/types
  analysis/desugar
  
namespaces
  Struct
  Field
  Variable
  // Function

properties 

  parameter-types of Variable: List(Type)
  
//in C functions and variables share the namespace
binding rules // Files
   
  File(_, _, _, _) : scopes Struct, Variable

binding rules // Struct and Fields
  StructDec(s, _) :
    defines Struct s of type s
    scopes Field

  Struct(s): refers to Struct s

  FieldDec(t, v)         : defines Field v of type t
  FieldArrayDec(t, v, _) : defines Field v of type t
  
  StructMember(v, Field(f)):
    refers to Field f in Struct s
    where v has type Struct(s)
    
binding rules // Variable
            
  VarDec(t, v)      : defines Variable v of type t
  ArrayDec(t, v, _) : defines Variable v of type t
      
  Arg(t, a)      : defines Variable a of type t
  ArrayArg(t, a) : defines Variable a of type t

  ArrayVar(v, _):
    refers to Variable v
         
  Var(v):
    refers to Variable v
    
  Field(f):
    refers to Field f
   
  CodeBlock(_): scopes Variable
  
binding rules // Function (shares the namespace with variable)
  
  // FunctionDef(t, f, params, _, _):
  //   defines Variable f of type (t, pt) in subsequent scope
  //   where params has type pt
  //   scopes Variable
  
  FunctionDef(t, f, p, _, _) :
    defines Variable f 
      of type t 
      of parameter-types pt
    where
      p has type pt
    scopes Variable  
  

  // Call(c, _): refers to Variable c
  
	Call(c, args) : 
	  refers to Variable c of parameter-types arg-types
	  where args has type arg-types
  

